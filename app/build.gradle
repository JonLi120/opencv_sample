plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.fulafula"
        minSdk 23
        targetSdk 31
        versionCode 3
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildFeatures{
        viewBinding true
    }

    def propertiesFile = rootProject.file("base.properties")
    def properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))

    signingConfigs {
        debug {
            storeFile properties['DEBUG_KEYSTORE_FILE'] != null ? file(properties['DEBUG_KEYSTORE_FILE']) : null
            keyAlias properties.getProperty('DEBUG_KEY_ALIAS', null)
            storePassword properties.getProperty('DEBUG_STORE_PASSWORD', null)
            keyPassword properties.getProperty('DEBUG_KEY_PASSWORD', null)
        }
        release {
            storeFile properties['KEYSTORE_FILE'] != null ? file(properties['KEYSTORE_FILE']) : null
            keyAlias properties.getProperty('KEY_ALIAS', null)
            storePassword properties.getProperty('STORE_PASSWORD', null)
            keyPassword properties.getProperty('KEY_PASSWORD', null)
        }
    }

    buildTypes {
        debug {
            debuggable true
            versionNameSuffix "-dev"
            manifestPlaceholders.appName = "@string/app_name_dev"
            manifestPlaceholders.analyticsEnabled = false
            manifestPlaceholders.crashlyticsEnabled = false

            signingConfig signingConfigs.debug

            buildConfigField 'String', 'API_DOMAIN', properties.getProperty('API_DOMAIN_DEV', "\"\"")
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders.appName = "@string/app_name"
            manifestPlaceholders.analyticsEnabled = true
            manifestPlaceholders.crashlyticsEnabled = true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField 'String', 'API_DOMAIN', properties.getProperty('API_DOMAIN_PROD', "\"\"")
        }
        sit {
            initWith debug
            versionNameSuffix "-sit"
            manifestPlaceholders.appName = "@string/app_name_sit"

            buildConfigField 'String', 'API_DOMAIN', properties.getProperty('API_DOMAIN_SIT', "\"\"")
        }
        uat {
            initWith release
            versionNameSuffix "-uat"
            manifestPlaceholders.appName = "@string/app_name_uat"

            buildConfigField 'String', 'API_DOMAIN', properties.getProperty('API_DOMAIN_UAT', "\"\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            jni {
                srcDirs 'src/main/jni', 'src/main/jniLibs'
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.2"
    implementation 'androidx.fragment:fragment-ktx:1.4.0'
    implementation 'androidx.activity:activity-ktx:1.4.0'

    // Render vector graphics.
    implementation "androidx.vectordrawable:vectordrawable:1.1.0"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'

    // RxJava
    implementation 'io.reactivex.rxjava3:rxjava:3.0.7'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.jakewharton.rxbinding4:rxbinding:4.0.0'

    // gson parser
    implementation 'com.google.code.gson:gson:2.8.9'

    // Database
    implementation "androidx.room:room-runtime:2.4.0"
    annotationProcessor "androidx.room:room-compiler:2.4.0"
    implementation "androidx.room:room-rxjava3:2.4.0"

    // viewModel and lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.0"
    implementation "androidx.lifecycle:lifecycle-process:2.4.0"

    // dependency injection
    implementation 'com.google.dagger:dagger:2.38.1'
    implementation 'com.google.dagger:dagger-android:2.38.1'
    implementation 'com.google.dagger:dagger-android-support:2.38.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.38.1'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.38.1'

    // for support biometric
    implementation 'androidx.biometric:biometric:1.1.0'

    // firebase
    implementation platform('com.google.firebase:firebase-bom:29.0.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-crashlytics'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // WindowManager
    implementation 'androidx.window:window:1.0.0-rc01'

    // CameraX core library using the camera2 implementation
    implementation 'androidx.camera:camera-core:1.1.0-alpha12'
    implementation 'androidx.camera:camera-camera2:1.1.0-alpha12'
    implementation "androidx.camera:camera-lifecycle:1.1.0-alpha12"
    implementation "androidx.camera:camera-view:1.0.0-alpha32"
    implementation "androidx.camera:camera-extensions:1.0.0-alpha32"

    // OpenCV
    implementation project(path: ':opencv')

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}